plugins {
	id 'maven'
	id 'java'
	id 'idea'
	id "com.jfrog.bintray" version "1.4"
	id 'me.champeau.gradle.jmh' version '0.2.0'
}

apply plugin: 'me.champeau.gradle.jmh'

defaultTasks 'build'
group = "me.nallar.whocalled"
version = "${buildVersion}"

repositories {
	jcenter()
}

dependencies {
	testCompile 'org.testng:testng:6.9.10'
}

test {
	useTestNG()
}

jmh {
	include = ''
	benchmarkMode = 'AverageTime'
	timeUnit = 'ns'
	iterations = 10
	fork = 0
}

// run gradle bintrayUpload -Pbintray=API_KEY_HERE
bintray {
	user = "nallar"
	key = project.hasProperty('bintray') ? project.bintray : ''
	configurations = ['archives']
	pkg {
		repo = 'maven'
		name = 'WhoCalled'
		version {
			name = project.version
		}
		licenses = ['MIT']
		vcsUrl = 'https://github.com/nallar/WhoCalled.git'
	}
}

tasks.withType(JavaCompile) {
	sourceCompatibility = 6
	targetCompatibility = 6
	options.with {
		encoding = 'UTF-8'
		compilerArgs <<
			"-XDignore.symbol.file=true" <<
			"-Xlint:all" <<
			"-Xlint:-path" <<
			"-Xlint:-rawtypes" <<
			"-Xlint:-processing"
		fork = true
		forkOptions.executable = 'javac'
	}
}

jar {
	baseName = project.name

	manifest.mainAttributes(
		"Built-By": System.properties['user.name'],
		"Created-By": System.properties['java.vm.version'] + " (" + System.properties['java.vm.vendor'] + ")",
		"Implementation-Title": name,
		"Implementation-Version": version
	)
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
	archives jar
}
